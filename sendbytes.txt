10 REM READ AND OUTPUT FILE
20 REM OPEN RS232 FIRST BECAUSE THIS ACTUALLY ERASES ALL BASIC VARIABLES.
30 REM OPEN RS232 AT 1200 BAUD
40 OPEN2,2,4,CHR$(8)+CHR$(0)
50 GET#2,S$ :REM TURN ON RECEIVER
60 OPEN 15,9,15:REM DISK CMD CHANNEL
70 PRINT"ENTER FILE NAME. DEFAULT:EXAMPLE":INPUT F$
80 IF F$="" THEN F$="EXAMPLE"
90 PRINT "FILE: ";F$
300 REM MSG TYPE, 1=START,2=CONT
310 MT=1  :REM MSG TYPE
320 EO=0  :REM USED TO HOLD STATUS
330 EN=0  :REM ERROR NUMBER IF ERROR
340 NB=0  :REM NUM BYTES TO SEND
350 MX=8  :REM MAX BYTES, MUST BE <8
360 DIM BYTES$(MX)
380 CS=0  :REM CHECKSUM
390 CU=0  :REM UPPER BYTE CHECKSUM
400 CL=0  :REM LOWER BYTE CHECKSUM
500 REM OPEN FILE TO BE SENT
510 OPEN5,9,8,"0:"+F$+",P,R"
520 GOSUB 59980 :REM CHECK FOR ERRORS
530 IF EN>1 GOTO 49980
540 GET#5,B$:REM READ A BYTE
550 EO=ST
570 IF B$="" THEN B$=CHR$(0)
580 BYTES$(NB)=B$
590 NB=NB+1
600 CS=CS+ASC(B$)
610 REM IF WE FILLED ARRAY, SEND BYTES
620 IF NB>=MX THEN GOSUB 1000
630 REM IF NOT EOF, KEEP GOING
640 IF EO<>64 GOTO 540
650 REM SEND ANY REMAINING BYTES
660 IF NB>0 THEN GOSUB 1000
665 PRINT""
670 PRINT "SEND COMPLETE"
680 GOTO 49980 :REM END PROG
1000 REM SEND BYTES
1010 GOSUB 3000
1035 MH=MT*2^5+NB
1040 REM SEND HEADER
1050 PRINT#2,CHR$(MH);
1080 REM SEND CHECKSUM LO BYTE
1090 PRINT#2,CHR$(CL);
1100 REM SEND CHECKSUM HI BYTE
1110 PRINT#2,CHR$(CU);
1120 FOR B=0 TO NB-1
1130 PRINT "w";
1140 PRINT#2,BYTES$(B);:REM SEND BYTE
1150 NEXT B
1160 REM WAIT UNTIL ALL BYTES SENT
1170 IF (PEEK(673)AND1)THEN 1170
1175 PRINT "q";
1180 NB=0
1200 CS=0
1210 CU=0
1220 CL=0
1230 MT=2 :REM WE ALREADY SENT FIRST
1240 RETURN
3000 REM CALCULATE CHECKSUM
3010 REM ONLY WORKS UP TO 12 BITS
3020 REM 1S COMPLEMENT
3030 CS=(CS+4095)-2*(CS AND 4095)
3040 CU=CS/256
3050 CL=CS AND 255
3070 RETURN
49980 REM EXIT CLEANLY
49990 PRINT"CLOSING RS232 AND FILE"
50000 CLOSE15:CLOSE5
50010 END
59980 REM READ ERROR CHANNEL
59990 INPUT#15,EN,EM$,ET,ES
60000 IF EN>1 THEN PRINT EN,EM$,ET,ES
60010 RETURN